apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = rootProject.ext["PUBLISH_GROUP_ID"]
version = rootProject.ext["PUBLISH_VERSION"]

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId rootProject.ext["PUBLISH_GROUP_ID"]
                artifactId rootProject.ext["PUBLISH_ARTIFACT_ID"].trim()
                version rootProject.ext["PUBLISH_VERSION"]

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                // Mostly self-explanatory metadata
                pom {
                    name = rootProject.ext["PUBLISH_ARTIFACT_ID"].trim()
                    description = 'Quash Android SDK'
                    url = 'https://github.com/Oscorp-HQ/QuashAndroidSDK'
                    licenses {
                        license {
                            name = 'Quash Liscence'
                            url = 'https://github.com/Oscorp -HQ/QuashAndroidSDK/blob/main/LICENSE.md'
                        }
                    }
                    developers {
                        developer {
                            id = 'ayushshrivastava'
                            name = 'Ayush Shrivastava'
                            email = 'ayush@quashbugs.com'
                        }
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        connection = 'scm:git:github.com/Oscorp-HQ/QuashAndroidSDK.git'
                        developerConnection = 'scm:git:ssh://github.com/Oscorp-HQ/QuashAndroidSDK.git'
                        url = 'https://github.com/Oscorp-HQ/QuashAndroidSDK.git'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}